import net.darkhax.curseforgegradle.TaskPublishCurseForge

import java.text.SimpleDateFormat

buildscript {
    repositories {
        maven {
            name = "Minecraft Forge maven"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "Parchment maven"
            url = "https://maven.parchmentmc.org"
        }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id "maven-publish"
    id 'net.darkhax.curseforgegradle' version '1.1.15'
    id 'org.jetbrains.changelog' version '1.2.1'
    id "com.modrinth.minotaur" version "2.+"
    id "org.jetbrains.kotlin.jvm" version "1.6.10"
    id "org.sonarqube" version "4.2.1.3168"
    id 'checkstyle'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

wrapper {
    gradleVersion = '7.5'
    distributionType = Wrapper.DistributionType.ALL
}

def isSnapshotVersion = Boolean.valueOf(System.getProperty("snapshot"))

version = "${mod_version}" + (isSnapshotVersion ? "-SNAPSHOT" : "")
group = 'de.srendi.advancedperipherals'

def static getenv(path = ".env") {
    def env = [:]

    def file = new File(path)
    if (file.exists()) {
        file.eachLine { line ->
            def (name, value) = line.tokenize("=")
            if (value != null) {
                env[name.trim()] = value.trim()
            }
        }
    }

    return env
}

def secretEnv = getenv()
def curseforgeKey = secretEnv["CURSEFORGE_KEY"] ?: System.getenv("CURSEFORGE_KEY") ?: ''
def modrinthKey = secretEnv["MODRINTH_KEY"] ?: System.getenv("MODRINTH_KEY")

sonarqube {
    properties {
        property "sonar.projectKey", "SirEndii_AdvancedPeripherals"
        property "sonar.organization", "sirendii"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

// Include resources generated by data generators.
sourceSets {
    main.resources {
        srcDir 'src/generated/resources'
    }
    testMod {}
}

minecraft {

    mappings channel: "${mappings_channel}", version: "${mappings_version}"

    accessTransformer file('src/main/resources/META-INF/accesstransformer.cfg')
    accessTransformer file('src/testMod/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            properties 'forge.logging.console.level': 'debug'

            mods {
                advancedperipherals {
                    source sourceSets.main
                }
            }
            if (project.hasProperty('mc_uuid')) {
                args '--uuid', project.getProperty('mc_uuid')
            }
            if (project.hasProperty('mc_username')) {
                args '--username', project.getProperty('mc_username')
            }
            if (project.hasProperty('mc_accessToken')) {
                args '--accessToken', project.getProperty('mc_accessToken')
            }
        }

        server {
            workingDirectory project.file('server')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.console.level', 'info'

            mods {
                advancedperipherals {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'advancedperipherals', '--all',
                    '--existing', file('src/main/resources').toString(),
                    '--existing', file('src/generated/resources').toString(),
                    '--output', file('src/generated/resources/')

            mods {
                advancedperipherals {
                    source sourceSets.main
                }
            }
        }

        testClient {
            workingDirectory project.file('test-files/client')
            parent runs.client

            mods {
                aptest {
                    source sourceSets.testMod
                }
            }

            lazyToken('minecraft_classpath') {
                (configurations.implementationExtra.copyRecursive().resolve())
                        .collect { it.absolutePath }
                        .join(File.pathSeparator)
            }
        }

        testServer {
            workingDirectory project.file('test-files/server')
            parent runs.server

            mods {
                aptest {
                    source sourceSets.testMod
                }
            }

            lazyToken('minecraft_classpath') {
                (configurations.implementationExtra.copyRecursive().resolve())
                        .collect { it.absolutePath }
                        .join(File.pathSeparator)
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = "Blamejared maven botania patchouli"
        url = 'https://maven.blamejared.com'
    }
    maven {
        name = "Squiddev maven cct"
        url = 'https://squiddev.cc/maven/'
    }
    /*maven {
        name = "Progwml6 maven jei"
        url = "https://dvs1.progwml6.com/files/maven/"
    }*/
    maven {
        name = "Theillusivec4 maven curios"
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name = "LDT Team minecolonies"
        url = 'https://ldtteam.jfrog.io/ldtteam/modding'
    }
    maven {
        name = "Modmaven"
        url = 'https://modmaven.dev/'
    }
    maven {
        name = "Create maven"
        url = "https://maven.tterrag.com/"
        content {
            includeGroup "com.simibubi.create"
            includeGroup "com.jozufozu.flywheel"
        }
    }
    maven {
        name = "Srendi Space repository"
        url "https://packages.intelligence-modding.de/maven/p/advanced-peripherals/ap-repository"
        credentials {
            username = spaceUsername
            password = spacePassword
        }
    }
    maven {
        name = "SirEdvin's private repository"
        url = "https://repo.repsy.io/mvn/siredvin/default"
        content {
            includeGroup "site.siredvin.ttoolkit"
        }
    }
    maven {
        name = "Shedaniel cloth"
        url = "https://maven.shedaniel.me/"
    }
    maven {
        url = uri("https://maven.pkg.github.com/refinedmods/refinedstorage")
        credentials {
            username = "anything"
            password = "\u0067hp_oGjcDFCn8jeTzIj4Ke9pLoEVtpnZMP4VQgaX"
        }
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

configurations {
    implementationExtra
    testModImplementation.extendsFrom(implementation)
    testModImplementation.extendsFrom(testImplementation)
}

dependencies {
    // Minimal requirements
    compileOnly "org.jetbrains:annotations:${jb_annotations}"
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("org.squiddev:cc-tweaked-${minecraft_version}:${cc_version}")
    // Compile-time API required
    // Ae2
    compileOnly fg.deobf("appeng:appliedenergistics2-forge:${appliedenergistics_version}")
    // Curios
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    // Minimal requirements end

    // Extended requirements
    // We don't use the api since we need a specific class from mekanism
    implementation fg.deobf("mekanism:Mekanism:${mekanism_version}")

    // Applied Energistics 2
    implementation fg.deobf("appeng:appliedenergistics2-forge:${appliedenergistics_version}")
    // Curios
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")

    // Refined Storage
    implementation fg.deobf("com.refinedmods:refinedstorage:${refinedstorage_version}") {
        transitive false
    }

    //AE2 Things
    compileOnly fg.deobf("curse.maven:ae2things-609977:${ae2things_version}-sources")
    //runtimeOnly fg.deobf("curse.maven:ae2things-609977:${ae2things_version}-sources")

    // Botania
    implementation fg.deobf("vazkii.botania:Botania:${botania_version}")

    // Minecolonies
    // Change to compileOnly when running `runData`
    implementation fg.deobf("com.ldtteam:minecolonies:${minecolonies_version}")
    implementation fg.deobf("com.ldtteam:structurize:${structurize_version}")
    implementation fg.deobf("com.ldtteam:multi-piston:${multipiston_version}")
    implementation fg.deobf("com.ldtteam:domum_ornamentum:${domumornamentum_version}:universal")
    implementation fg.deobf("com.ldtteam:blockui:${blockui_version}")

    //Patchouli
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")

    // Create
    implementation fg.deobf("com.simibubi.create:create-${minecraft_version}:${create_version}:all")

    //Removed until fully ported
    //testImplementation fg.deobf("site.siredvin.ttoolkit:ttoolkit-${minecraft_version}:${ttoolkit_version}")

    //Powah
    implementation fg.deobf("curse.maven:powah-633483:${powah_version}")
    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:8.2.88")
    implementation fg.deobf("dev.architectury:architectury-forge:6.2.43")

    implementation fg.deobf("curse.maven:crash-utilities-371813:4406293")

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit_version}"
    testImplementation "org.hamcrest:hamcrest:${hamcrest_version}"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${kotlinx_coroutines_version}"
    testModImplementation sourceSets.main.output

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    // Testing stuff
    // JEI
    implementation fg.deobf("mezz.jei:jei-${jei_version}")
}


changelog {
    version = "${project.version}"
    path = "${project.projectDir}/CHANGELOG.md"
    header = "[${minecraft_version}-${-> version.get()}] - ${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}"
    //Valid patterns would be
    //1.16.5-1.1.1.1b
    //1.18-1.0r
    //1.19-1.0.0a
    headerParserRegex = ~/\d+(\.\d+){1,2}-\d+(\.\d+){1,3}\w/
    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "[Unreleased]"
    groups = []
}

compileTestModJava {
    dependsOn(compileJava)
}

task setupServer(type: Copy) {
    group "test server"
    description "Sets up the environment for the test server."

    from("src/testMod/server-files") {
        include "eula.txt"
        include "server.properties"
    }
    into "test-files/server"
}

["Client", "Server"].forEach { name ->
    tasks.register("test$name", JavaExec.class).configure {
        it.group('In-game tests')
        it.description("Runs tests on a temporary Minecraft instance.")
        it.dependsOn(setupServer, "prepareRunTest$name", "cleanTest$name", 'compileTestModJava')

        JavaExec exec = tasks.getByName("runTest$name")
        exec.copyTo(it)
        it.setClasspath(exec.getClasspath())
        it.mainClass = exec.mainClass
        it.setArgs(exec.getArgs())

        it.systemProperty('forge.logging.console.level', 'debug')
        it.systemProperty('ttoolkit.run', 'true')
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

afterEvaluate {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "2000"
    }
}

jar {
    manifest({
        attributes(["Specification-Title"     : "advancedperipherals",
                    "Specification-Vendor"    : "Srendi",
                    "Specification-Version"   : "${version}",
                    "Implementation-Title"    : "advancedperipherals",
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "Srendi",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    })
    archiveFileName = "AdvancedPeripherals-${minecraft_version}-${mod_version}.jar"
}

jar.finalizedBy('reobfJar')

tasks.register('publishCurseForge', TaskPublishCurseForge, { task ->
    setGroup("publishing")
    setDescription("Upload Advabced Oeruogeraks to CurseForge")

    // This token is used to authenticate with CurseForge. It should be handled
    // with the same level of care and security as your actual password. You
    // should never share your token with an untrusted source or publish it
    // publicly to GitHub or embed it within a project. The best practice is to
    // store this token in an environment variable or a build secret.
    apiToken = curseforgeKey

    // Tells CurseForgeGradle to publish the output of the jar task. This will
    // return a UploadArtifact object that can be used to further configure the
    // file.
    task.upload(431725, jar) { file ->
        try {
            file.changelog = "${project.changelog.get("${minecraft_version}-${project.version}").withHeader(false).toText()}"
            file.changelogType = "markdown"
        } catch (err) {
            System.out.println(err)
            file.changelog = ""
        }

        addOptional(
                'applied-energistics-2', 'curios', 'mekanism', 'refined-storage'
        )

        addRequirement(
                'cc-tweaked'
        )

        releaseType = "${release_type}"
    }
    debugMode = true
})

modrinth {
    token = modrinthKey // This is the default. Remember to have the MODRINTH_TOKEN environment variable set or else this will fail, or set it to whatever you want - just make sure it stays private!
    projectId = 'SOw6jD6x' // This can be the project ID or the slug. Either will work!
    versionNumber = "${minecraft_version}-${project.version}"
    versionName = "Advanced Peripherals ${minecraft_version} ${version}"
    versionType = release_type
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    gameVersions = [minecraft_version] // Must be an array, even with only one version
    loaders = ["forge"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradl
    try {
        changelog = "${project.changelog.get("${minecraft_version}-${project.version}").withHeader(false).toText()}"
    } catch (err) {
        System.out.println(err)
        changelog = ""
    }
}

publishing {
    repositories {
        maven {
            name="space"
            url "https://packages.intelligence-modding.de/maven/p/advanced-peripherals/ap-repository"
            credentials {
                username = spaceUsername
                password = spacePassword
            }
        }
        maven {
            name="github"
            url "https://maven.pkg.github.com/SirEndii/AdvancedPeripherals"
            credentials {
                username = secretEnv["GH_ACTOR"] ?: System.getenv("GH_ACTOR") ?: ''
                password = secretEnv["GH_TOKEN"] ?: System.getenv("GH_TOKEN") ?: ''
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId = "advancedperipherals"
            from(components.java)
        }
    }
}